plugins {
    id 'java'
    id 'com.diffplug.spotless' version '6.22.0'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.surajkumar'
version = '1.2.0'

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

spotless {
    java {
        importOrder()
        googleJavaFormat()
                .aosp()
                .reflowLongStrings()
                .formatJavadoc(true)
                .reorderImports(true)
                .groupArtifact('com.google.googlejavaformat:google-java-format')
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-api:2.22.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.22.1'

    testImplementation 'org.mockito:mockito-core:5.8.0'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = 'io.github.surajkumar'
            artifactId = 'concurrency-library'
            version = '1.0.0'

            pom {
                name = 'Concurrency Library'
                description = 'A library for concurrent programming'
                url = 'https://github.com/surajkumar/concurrency-library'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'surajkumar'
                        name = 'Suraj Kumar'
                        email = '76599223+surajkumar@users.noreply.github.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/surajkumar/concurrency-library.git'
                    developerConnection = 'scm:git:ssh://github.com/surajkumar/concurrency-library.git'
                    url = 'https://github.com/surajkumar/concurrency-library'
                }
            }
        }
    }

    repositories {
        maven {
            url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = project.findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = project.findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}